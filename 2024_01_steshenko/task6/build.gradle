plugins {
    id 'java'
    id 'application'
}

tasks.register('clientJar', Jar) {
    manifest {
        attributes 'Main-Class': 'focus.start.task6.client.ClientMain'
    }
    exclude('**/server/**')
    archiveBaseName = 'client'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

tasks.register('serverJar', Jar) {
    manifest {
        attributes 'Main-Class': 'focus.start.task6.server.ServerMain'
    }
    exclude('**/client/**')
    archiveBaseName = 'server'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

group = 'focus.start.task6'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.17.1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.14'
}

test {
    useJUnitPlatform()
}